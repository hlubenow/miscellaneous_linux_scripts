#!/usr/bin/perl

=begin comment

    m - Play multimedia-file(s), version 1.3

        Including "playfrom", when started with "m 7", for example.

    Copyright (C) 2021, 2023, 2024, 2026 hlubenow

    This program is free software: you can redistribute it and/or modify
     it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.

=end comment

=cut

use warnings;
use strict;

package FilePlayer {

    use Cwd;

    sub new {
        my $classname = shift;
        my $self = {};
        $self->{dir}   = getcwd();
        $self->{mpl}   = "mplayer -af volnorm -really-quiet";
        $self->{mplfs} = "mplayer -fs -af volnorm -really-quiet";
        $self->{m321}  = "mpg123 -q";
        $self->{xmp}   = "xmp";
        $self->{sufs}  = {"flv"      => $self->{mplfs},
                          "flv.part" => $self->{mplfs},
                          "webm"     => $self->{mplfs},
                          "mp3"      => $self->{m321},
                          "mod"      => $self->{xmp},
                          "s3m"      => $self->{xmp},
                          "it"       => $self->{xmp},
                          "ogg"      => "ogg123"};

        $self->{checks} = {"WAVE"    => "aplay -q",
                           "MP3"     => $self->{m321},
                           "layer"   => $self->{m321},
                           "AVI"     => $self->{mplfs},
                           "Matroska" => $self->{mplfs},
                           "Microsoft ASF"         => $self->{mplfs},
                           "Apple QuickTime movie" => $self->{mplfs},
                           "MP4"                   => $self->{mplfs},
                           "tracker" => "xmp",
                           "SID"     => "sidplayfp",
                           "MIDI"    => "timidity 1>/dev/null"};

        return bless($self, $classname);
    }

    sub getFileList {
        my $self = shift;
        my @fl;
        if ($#ARGV < 0) {
            @fl = <*>;
        } elsif ($#ARGV == 0 && $ARGV[0] !~ /\D/) {
            $self->getFileListPlayFrom();
            return;
        } else {
            @fl = @ARGV;
        }
        $self->{filelist} = \@fl;
    }

    sub getFileListPlayFrom {
        # This function is only called, if m was started with
        # one option, and that option is a number.
        my $self = shift;
        print "Performing 'playfrom'.\n";
        my @fl = <*>;
        my @result = ();
        my %nums   = ();
        my @numskeys;
        my @b;
        my ($d, $dlen, $c, $i, $u, $num);
        for $i (@fl) {
            $num = "";
            @b = split(/\./, $i);
            $d = $b[0];
            $dlen = length($d) - 1;
            for $u (0 .. $dlen) {
                $c = substr($d, $u, 1);
                if ($c =~ /\D/) {
                    last;
                } else {
                    $num .= $c;
                }
            }
            if ($num ne "" && $num >= $ARGV[0]) {
                $nums{$num} = $i;
            }
        }
        @numskeys = sort {$a <=> $b} keys(%nums);
        for $i (@numskeys) {
            push(@result, $nums{$i});
        }
        $self->{filelist} = \@result;
    }

    sub getCommand {
        my $self = shift;
        my $fname = shift;
        my %sufs  = %{ $self->{sufs} };
        my @sufkeys = keys(%sufs);
        my %checks = %{ $self->{checks} };
        my @checkskeys = keys(%checks);
        my ($i, $fstr);
        foreach $i (@sufkeys) {
            if ($fname =~ /\.\Q$i\E$/) {
                return $sufs{$i};
            }
        }
        $fstr = `file "$fname"`;
        for $i (@checkskeys) {
            if ($fstr =~ /\Q$i\E/) {
                return $checks{$i};
            }
        }
        # Special cases:
        if($fstr =~ /MPEG/) {
            return $self->{mplfs};
        }
        if($fstr =~ /data/ && $fname =~ /sid$/) {
            return "sidplay2";
        }
        return "";
    }

    sub playFiles {
        my $self = shift;
        $self->getFileList();
        my @flist = @{ $self->{filelist} };
        my $i;
        my $command = "";
        foreach $i (@flist) {
            if (! -f $i) {
                print "'$i' not suitable (not existent or directory).\n";
                next;
            }
            $command = $self->getCommand($i);
            if ($command eq "") {
                print "Format of '$i' not recognized.\n";
                next;
            }
            if ($command eq $self->{xmp}) {
                print "\nPlayer is $self->{xmp}.\n\n";
            }
            $self->playFile($command, $i);
        }
    }

    sub playFile {
        my $self = shift; 
        my $command = shift;
        my $fname = shift;
        my $e;
        $e = "$command \"$fname\"";
        print "Playing '$fname'.\n";
        system($e);
    }
}

my $fp = FilePlayer->new();
$fp->playFiles();
